apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.devspark.robototextview.gradle-plugin'
apply plugin: 'com.getkeepsafe.dexcount'

//noinspection GroovyAssignabilityCheck
if (project.hasProperty("LixoZero.signing")
        && new File(project.property("LixoZero.signing") + ".gradle").exists()) {
    apply from: project.property("LixoZero.signing") + ".gradle";
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "br.com.semanalixozero.app"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 3
        versionName "1.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "LixoZero-$versionName")
        resConfigs "pt-rBR"
        manifestPlaceholders = [applicationLabel: "@string/app_name"]
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            manifestPlaceholders = [applicationLabel: "SLZ Dev"]
        }
        release {
            debuggable false
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    support = '26.0.2'
    butterknife = '8.8.1'
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile 'junit:junit:4.12'

    // Support
    compile "com.android.support:appcompat-v7:${support}"
    compile "com.android.support:design:${support}"
    compile "com.android.support:cardview-v7:${support}"
    compile "com.android.support:recyclerview-v7:${support}"
    compile "com.android.support:customtabs:${support}"

    // View Injection
    compile "com.jakewharton:butterknife:${butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknife}"

    // Roboto Fonts
    compile 'com.github.johnkil.android-robototextview:robototextview:4.0.0'

    // Custom Bottom Navigation Component
    compile 'devlight.io:navigationtabbar:1.2.5'

    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    compile 'com.timehop.stickyheadersrecyclerview:library:0.4.3@aar'

    compile 'com.jakewharton.timber:timber:4.5.1'

    // Material Feature discovery
    compile 'com.getkeepsafe.taptargetview:taptargetview:1.10.0'

    // Fabric
    compile('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
    compile('com.crashlytics.sdk.android:answers:1.4.1@aar') {
        transitive = true;
    }
}

robototextview {
    include 'Roboto', 'RobotoSlab'
}

retrolambda {
    javaVersion JavaVersion.VERSION_1_7
}

dexcount {
    format = "list"
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = true
    maxMethodCount = 65536
}